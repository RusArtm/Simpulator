import src/common;
import src/state;

export {
	makeDisplay(state : SimpulatorState) -> Material;
}

makeDisplay(state : SimpulatorState) -> Material {
	MLet(
		"content",
		MFixSize(
			MCenter(MSelect(state.expressionB, \expr -> MCols([
				MFillX(),
				makeExpressionMaterial(expr),
				MFixedX(16.0),
				MFixSize(makeEvaluatedText(expr), MFixed(opButtonWidht, buttonHeight))
			]))),
			displaySize
		),
		MGroup([
			TRectangle([Stroke(0xffffff), StrokeWidth(2.0)], TGhost("content")),
			MDisplay("content")
		])
	);
}

makeExpressionMaterial(expressionM : Maybe<Expression>) -> Material {
	eitherMap(expressionM, \expression -> MText(expression2str(expression), []), MEmpty());
}

expression2str(expression : Expression) -> string {
	switch (expression) {
		OpValue(val): {
			i2s(trunc(val));
		}
		BinaryOperation(op1, op2, type): {
			formatString(
				"%1%2%3 %4 %5",
				[
					if (type == Multiplication()) "(" else "",
					expression2str(op1),
					if (type == Multiplication()) ")" else "",
					opType2str(type),
					expression2str(op2),
				]
			);
		}
	}
}

makeEvaluatedText(expressionM : Maybe<Expression>) -> Material {
	eitherMap(
		expressionM,
		\expr -> {
			val = i2s(trunc(evaluate(expr)));
			MText("= " + val, []);
		},
		MEmpty()
	);
}
